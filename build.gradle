plugins {
	id 'application'
	id 'org.jetbrains.kotlin.jvm' version '1.4.10'
	id 'com.github.ben-manes.versions' version '0.35.0'
	id 'org.openjfx.javafxplugin' version '0.0.9'
	id 'org.beryx.runtime' version '1.11.4'
	id 'com.github.johnrengelman.shadow' version '6.1.0'
}

repositories {
	mavenCentral()
	mavenLocal()
	jcenter()
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

application {
	mainClassName = "github.macro.LauncherKt"
	applicationName = "Simple-Exile-Builds"
}

def loggerVersion = '2.13.3'
def jacksonVersion = '2.11.3'

dependencies {
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	implementation group: 'no.tornado', name: 'tornadofx', version: "2.0.0-SNAPSHOT"
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jacksonVersion"
	implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: "$jacksonVersion"
	implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: "$jacksonVersion"
	implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: "$loggerVersion"
	runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: "$loggerVersion"
}

compileKotlin {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11

	kotlinOptions {
		jvmTarget = "11"
		apiVersion = "1.4"
		languageVersion = "1.4"
		suppressWarnings = true
	}
}

compileTestKotlin {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11

	kotlinOptions {
		jvmTarget = "11"
		apiVersion = "1.4"
		languageVersion = "1.4"
		suppressWarnings = true
	}
}

javafx {
	version = "15.0.1"
	modules = ['javafx.controls']
}

jar {
	manifest.attributes provider: 'gradle'
	manifest.attributes 'Main-Class': mainClassName
	manifest.attributes 'Multi-Release': true
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

runtime {
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
	launcher {
		noConsole = false
	}
}

run {
	standardInput = System.in
}

clean {
	delete 'release'
}

task copyApplication(type: Copy) {
	dependsOn 'runtime'

	from "$buildDir/image"
	include '**/*'
	into "$buildDir/release"
}

task copyResources(type: Copy) {
	from 'resources'
	exclude 'docs'
	include '**/*'
	into "$buildDir/release/bin/resources"
}

task releaseApplication(type: Zip) {
	dependsOn 'copyApplication', 'copyResources'

	from "$buildDir/release"
	include '**/*'
	archiveFileName = 'Simple-Exile-Builds.zip'
	destinationDir(file('release'))
}

task releaseResources(type: Zip) {
	dependsOn 'copyResources'

	from "$buildDir/release/bin/resources"
	include '**/*'
	archiveFileName = 'Simple-Exile-Builds_resources.zip'
	destinationDir(file('release'))
}

task release {
	dependsOn 'releaseResources', 'releaseApplication'
	releaseApplication.shouldRunAfter(releaseResources)
}

def isNonStable = { String version ->
	def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
	def regex = /^[0-9,.v-]+(-r)?$/
	return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
	checkForGradleUpdate = false

	// Example 1: reject all non stable versions
	rejectVersionIf {
		isNonStable(candidate.version)
	}

	// Example 2: disallow release candidates as upgradable versions from stable versions
	rejectVersionIf {
		isNonStable(candidate.version) && !isNonStable(currentVersion)
	}

	// Example 3: using the full syntax
	resolutionStrategy {
		componentSelection {
			all {
				if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
					reject('Release candidate')
				}
			}
		}
	}
}